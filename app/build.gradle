apply plugin: 'com.android.application'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.martinandersson.simplerecyclerviewapp"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError true
        warningsAsErrors true
        checkReleaseBuilds true
        lintConfig file('lint.xml')
        htmlReport true
    }

}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/volley.jar')
    compile 'com.android.support:support-v13:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile "com.android.support:appcompat-v7:22.2.0"

    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.github.bumptech.glide:glide:3.6.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup:otto:1.3.8'

    compile 'com.fasterxml.jackson.core:jackson-core:2.6.0-rc2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.0-rc2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.0-rc2'

    compile 'com.jakewharton:butterknife:7.0.0'

    // Testing-only dependencies
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
}


// Add checkstyle, findbugs, pmd and lint to the check task.
check.dependsOn 'checkstyle', 'findbugs', 'pmd', 'lint'

//  Usage:
//      ./gradlew app:findbugs
//      open app/build/reports/findbugs/findbugs.html
task findbugs(type: FindBugs) {
    excludeFilter file("${project.rootDir}/config/findbugs/exclude.xml")
    classes = fileTree('build/intermediates/classes/debug/')
    // Varies based on your app build configs and flavors...
    source = fileTree('src/main/java/')
    classpath = files()
    effort = 'max'
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

//  Usage:
//      ./gradlew app:pmd
//      open app/build/reports/pmd/pmd.html
task pmd(type: Pmd) {
    ruleSetFiles = files("${project.rootDir}/config/pmd/pmd-ruleset.xml")
    ignoreFailures = false
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/*Tests.java'
    exclude '**/*TestCase.java'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$project.buildDir/reports/pmd/pmd.xml"
        }
        html {
            destination "$project.buildDir/reports/pmd/pmd.html"
        }
    }
}

//  Usage:
//      ./gradlew app:checkstyle
//      open app/build/reports/checkstyle/checkstyle.xml
task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}